
def getRepositoryUsername() {
    return hasProperty('mavenUsername') ? mavenUsername : ""
}
        
def getRepositoryPassword() {
    return hasProperty('mavenPassword') ? mavenPassword : ""
}

allprojects() {
    group = 'edu.cmu.sphinx'
    version = '5prealpha-SNAPSHOT'
}

subprojects() {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                artifactId = project.name
                version = project.version
            }
        }
        repositories {
            maven {
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = getRepositoryUsername()
                    password = getRepositoryPassword()
                }
            }
            maven {
                url = "https://oss.sonatype.org/content/repositories/snapshots/"
                credentials {
                    username = getRepositoryUsername()
                    password = getRepositoryPassword()
                }
            }
        }
    }


    compileJava.options.encoding = 'UTF-8'
    javadoc.options.encoding = 'UTF-8'
    sourceCompatibility = 1.7
    targetCompatibility = 1.7


    task javadocJar(type: Jar) {
        archiveClassifier.set('javadoc')
        from javadoc
    }

    task packageSources(type: Jar) {
        archiveClassifier.set('sources')
        from sourceSets.main.allSource
    }

    artifacts.archives packageSources, javadocJar
}
